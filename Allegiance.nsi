; Script generated by the HM NIS Edit Script Wizard.

SetCompressor lzma

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Free"
!define PRODUCT_VERSION "Allegiance"
!define PRODUCT_PUBLISHER "The Free Allegiance Organization"
!define PRODUCT_WEB_SITE "http://www.freeallegiance.org"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_WELCOMEFINISHPAGE_BITMAP "C:\Installer\Resources\Bitmaps\splash.bmp"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_RIGHT
!define MUI_HEADERIMAGE_BITMAP "C:\Installer\Resources\Bitmaps\header.bmp"
!define MUI_ICON "C:\Installer\Resources\Bitmaps\allegb.ico"
!define MUI_UNICON "C:\Installer\Resources\Bitmaps\allegb.ico"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\readme.rtf"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "C:\Program Files\Microsoft Games\Allegiance\EULA.RTF"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

SetFont "Tahoma" 8
BrandingText "Free Allegiance R5 (Installer Build 258)"

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Allegiance Setup.exe"
InstallDir "$PROGRAMFILES\Microsoft Games\Allegiance"
ShowInstDetails show
ShowUnInstDetails show

Section "Allegiance Game" SEC01
  SectionIn RO
  SetDetailsPrint both
  DetailPrint "Extracting Files..."
  SetDetailsPrint textonly
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File /r "C:\Program Files\Microsoft Games\Allegiance\*.*"
  SetDetailsPrint listonly
  DetailPrint "- Files Extracted!"
SectionEnd

Section "Install English C++ Runtime" SEC03
  SetDetailsPrint both
  DetailPrint "Installing C++ Runtime..."
  SetDetailsPrint listonly
  DetailPrint "(this will take several minutes)"
  SetDetailsPrint none
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "C:\Installer\Resources\Redist-x86\vcredist_x86.exe"
  ExecWait 'vcredist_x86.exe /q:a /c:"VCREDI~3.EXE /q:a /c:""msiexec /i vcredist.msi /qn"" "'
  Delete "$INSTDIR\vcredist_x86.exe"
  SetDetailsPrint listonly
  DetailPrint "- C++ Runtime Installed!"
SectionEnd

Section -TypeLibraries
  SetDetailsPrint both
  DetailPrint "Registering Type Libraries..."
  SetDetailsPrint none
  SetOutPath $INSTDIR
  RegDLL "$INSTDIR\ATL.DLL"
  RegDLL "$INSTDIR\ATL71.DLL"
  RegDLL "$INSTDIR\MFC42.DLL"
  RegDLL "$INSTDIR\MFC71.DLL"
  SetDetailsPrint listonly
  DetailPrint "- Libraries Registered!"
SectionEnd

Section /o "Unpack OGG Files" SEC02
  SetDetailsPrint both
  DetailPrint "Decoding audio files... "
  SetDetailsPrint listonly
  DetailPrint "(this will take several minutes)"
  SetDetailsPrint none
  SetOutPath "$INSTDIR\Artwork"
  ExecWait '"$INSTDIR\Artwork\oggdec.exe" "$INSTDIR\Artwork\*.ogg"'
  SetDetailsPrint listonly
  DetailPrint "- Audio Decoded!"
SectionEnd

Section -AdditionalIcons
  SetDetailsPrint both
  DetailPrint "Creating Icons..."
  SetDetailsPrint none
  SetOutPath "$INSTDIR"
  CreateShortCut "$DESKTOP\Allegiance.lnk" "$INSTDIR\ASGSClient.exe"
  CreateShortCut "$DESKTOP\Allegiance Learning Guide.lnk" "http://www.freeallegiance.org/FAW/index.php/Learning_guide" "" "$INSTDIR\academy.ico"
  CreateDirectory "$SMPROGRAMS\Allegiance"
  CreateShortCut "$SMPROGRAMS\Allegiance\Allegiance.lnk" "$INSTDIR\ASGSClient.exe"
  CreateShortCut "$SMPROGRAMS\Allegiance\Allegiance Learning Guide.lnk" "http://www.freeallegiance.org/FAW/index.php/Learning_guide" "" "$INSTDIR\academy.ico"
  SetDetailsPrint listonly
  DetailPrint "- Icons Created!"
SectionEnd

Section -Post
  SetDetailsPrint both
  DetailPrint "Creating Uninstaller..."
  SetDetailsPrint textonly
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "ArtPath" "$INSTDIR\Artwork"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "EXE Path" "$INSTDIR"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "CfgFile" "http://autoupdate.alleg.net/allegiance.cfg"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "CDKey" "FERAL-1234567890123456"
  WriteRegDWORD HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "FIRSTRUN" "1"
  WriteRegDWORD HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.0" "HasTrained" "1"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "ArtPath" "$INSTDIR\Artwork"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "EXE Path" "$INSTDIR"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "CfgFile" "http://fazdev.alleg.net/FAZ/FAZbeta.cfg"
  WriteRegStr HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "CDKey" "FERAL-1234567890123456"
  WriteRegDWORD HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "FIRSTRUN" "1"
  WriteRegDWORD HKLM "Software\Microsoft\Microsoft Games\Allegiance\1.1" "HasTrained" "1"
  SetDetailsPrint both
  DetailPrint "- Installation Complete!"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Includes all required components to play Allegiance."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Transcodes the game audio from OGG to WAV.$\r$\rThis option is only recommended for slow computers and will increase install time."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC03} "Visual C++ 2008 SP1 ENGLISH Runtime.$\r$\rDO NOT INSTALL THIS IF YOUR COMPUTER'S LANGUAGE IS NOT ENGLISH!$\r$\rIf you've installed these runtimes in the past, you do not need to install them again.$\r$\rIf you are not sure and have an English computer, leave this item selected."
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "NOTICE: This uninstaller will remove ALL files and directories in the $(^Name) directory, including ones not placed there by the installer.  If you wish to save any of those files, click NO and back them up before proceeding.$\r$\rAre you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Function .onInit
  ReadRegStr $0 HKLM "SOFTWARE\Microsoft\.NETFramework\policy\v2.0" "50727"
  IfErrors 0 DotNetInstalled
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) requires the Microsoft .NET framework version 2.0.$\r$\rYou can install it via WindowsUpdate, at http://windowsupdate.microsoft.com."
  Abort
  DotNetInstalled:
FunctionEnd

Section Uninstall
  SetDetailsPrint both
  DetailPrint "Please wait while Allegiance is uninstalled."
  SetDetailsPrint textonly
  SetOutPath "$INSTDIR"
  Delete "$DESKTOP\Allegiance.lnk"
  Delete "$DESKTOP\Allegiance Academy.lnk"
  Delete "$SMPROGRAMS\Allegiance\Allegiance.lnk"
  Delete "$SMPROGRAMS\Allegiance\Allegiance Academy.lnk"

  RMDir /r "$SMPROGRAMS\Allegiance"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd